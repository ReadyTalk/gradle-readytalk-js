plugins {
  id 'groovy'
  id 'jacoco'
  id 'java-gradle-plugin'
  id 'maven-publish'
  id 'com.gradle.plugin-publish' version '0.9.0'
  id 'com.jfrog.artifactory' version '3.0.1'
  id 'com.github.kt3k.coveralls' version '2.3.1'
  id 'com.readytalk.ci' version '0.3.0'
}

ext {
  isRelease = buildEnv.travisTag ==~ /v\d+\.\d+\.\d+/
  isMaster = buildEnv.travisBranch == "master" && buildEnv.travisPullRequest == 'false'
  isReleaseBranch = buildEnv.travisBranch ==~ /release_\d+\.\d+\.\d+/
}

if (!isRelease) {
  version = version + '-SNAPSHOT'
}

defaultTasks 'build'

repositories {
  mavenLocal()
  jcenter()
  maven {
    name 'JFrog OSS snapshot repo'
    url  'https://oss.jfrog.org/oss-snapshot-local/'
  }
}

dependencies {
  compile gradleApi()
  compile 'com.moowork.gradle:gradle-node-plugin:0.9'
  compile 'com.moowork.gradle:gradle-grunt-plugin:0.9'

  testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
    exclude module: 'groovy-all'
  }
}

pluginBundle {
  website = 'http://oss.readytalk.com/gradle-readytalk-js'
  vcsUrl = 'https://github.com/ReadyTalk/gradle-readytalk-js.git'
  description = 'Gradle plugin for ReadyTalk-style JavaScript projects.'

  plugins {
    readytalkJsPlugin {
      id = 'com.readytalk.js'
      displayName = 'ReadyTalk JS Plugin'
      tags = [ 'java', 'gradle', 'groovy', 'node', 'javascript' ]
    }
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact publishPluginJar {
        classifier 'sources'
      }
      artifact publishPluginGroovyDocsJar {
        classifier 'groovydoc'
      }
      artifact publishPluginJavaDocsJar {
        classifier 'javadoc'
      }
    }
  }
}

jacocoTestReport {
  reports {
    xml.enabled = true // coveralls plugin depends on xml format report
    html.enabled = true
  }
}

check.dependsOn 'jacocoTestReport'

tasks.coveralls {
  dependsOn 'check'
  onlyIf { buildEnv.isCI }
}

tasks.ci.dependsOn tasks.coveralls

artifactoryPublish.onlyIf { isMaster || isReleaseBranch }

if (isRelease) {
  publish.dependsOn 'publishPlugins'
}

artifactory {
  contextUrl = "http://oss.jfrog.org/artifactory"

  publish {
    repository {
      repoKey = 'oss-snapshot-local'
      username = bintrayUser
      password = bintrayKey
      maven = true
    }
  }
}

wrapper.gradleVersion = '2.3'
