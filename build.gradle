plugins {
  id 'groovy'
  id 'jacoco'
  id 'java-gradle-plugin'
  id 'nu.studer.plugindev' version '1.0.3'
  id 'com.jfrog.artifactory' version '3.0.1'
  id 'com.github.kt3k.coveralls' version '2.0.1'
  id 'com.readytalk.ci' version '0.2.3'
}

ext {
  isRelease = buildEnv.travisTag ==~ /v\d+\.\d+\.\d+/
  isMaster = buildEnv.travisBranch == "master" && buildEnv.travisPullRequest == 'false'
  isReleaseBranch = buildEnv.travisBranch ==~ /release_\d+\.\d+\.\d+/
}

if (!isRelease) {
  version = version + '-SNAPSHOT'
}

defaultTasks 'build'

repositories {
  maven {
    name 'JFrog OSS snapshot repo'
    url  'https://oss.jfrog.org/oss-snapshot-local/'
  }
}

dependencies {
  compile gradleApi()
  compile 'com.moowork.gradle:gradle-node-plugin:0.9'
  compile 'com.moowork.gradle:gradle-grunt-plugin:0.9'

  testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
    exclude module: 'groovy-all'
  }
}

plugindev {
  pluginImplementationClass 'com.readytalk.gradle.js.JsPlugin'
  pluginDescription 'Gradle plugin for ReadyTalk-style JavaScript projects.'
  pluginLicenses 'MIT'
  pluginTags 'java', 'gradle', 'groovy', 'node', 'javascript'
  authorId 'dougborg'
  authorName 'Douglas Borg'
  authorEmail 'dougborg@dougborg.org'
  projectUrl = 'http://oss.readytalk.com/gradle-readytalk-js'
  projectIssuesUrl = 'https://github.com/ReadyTalk/gradle-readytalk-js/issues'
  projectVcsUrl = 'https://github.com/ReadyTalk/gradle-readytalk-js.git'
  projectInceptionYear '2014'
  done() // do not omit this
}

jacocoTestReport {
  reports {
    xml.enabled = true // coveralls plugin depends on xml format report
    html.enabled = true
  }
}

check.dependsOn 'jacocoTestReport'

tasks.coveralls {
  dependsOn 'check'
  onlyIf { buildEnv.isCI }
}

tasks.ci.dependsOn tasks.coveralls

artifactoryPublish.onlyIf { isMaster || isReleaseBranch }
bintrayUpload.onlyIf { isRelease }

artifactory {
  contextUrl = "http://oss.jfrog.org/artifactory"

  publish {
    repository {
      repoKey = 'oss-snapshot-local'
      username = bintrayUser
      password = bintrayKey
      maven = true
    }
  }
}

bintray {
  user = bintrayUser
  key = bintrayKey
  dryRun = !isRelease
  pkg {
    userOrg = 'readytalk'
    repo = 'plugins'
    version.vcsTag = buildEnv.travisTag
  }
}

wrapper.gradleVersion = '2.3'
