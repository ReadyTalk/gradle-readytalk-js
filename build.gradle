plugins {
  id 'groovy'
  id 'jacoco'
  id 'java-gradle-plugin'
  id 'maven-publish'
  id 'com.gradle.plugin-publish' version '0.9.1'
  id 'com.jfrog.artifactory' version '3.1.1'
  id 'com.github.kt3k.coveralls' version '2.4.0'
  id 'com.readytalk.ci' version '0.5.3'
  id 'nebula.info' version '2.2.4'
}

apply plugin: 'com.readytalk.ci.version.snapshot'

defaultTasks 'build'

repositories {
  mavenLocal()
  jcenter()
  maven {
    name 'JFrog OSS snapshot repo'
    url  'https://oss.jfrog.org/oss-snapshot-local/'
  }
}

dependencies {
  compile gradleApi()
  compile 'com.moowork.gradle:gradle-node-plugin:0.11'
  compile 'com.moowork.gradle:gradle-grunt-plugin:0.11'
  compile 'com.moowork.gradle:gradle-gulp-plugin:0.11'

  testCompile('com.netflix.nebula:nebula-test:3.1.0') {
    exclude module: 'groovy-all'
  }

}

pluginBundle {
  website = 'http://oss.readytalk.com/gradle-readytalk-js'
  vcsUrl = 'https://github.com/ReadyTalk/gradle-readytalk-js.git'
  description = 'Gradle plugin for ReadyTalk-style JavaScript projects.'

  plugins {
    readytalkJsPlugin {
      id = 'com.readytalk.js'
      displayName = 'ReadyTalk JS Plugin'
      tags = [ 'java', 'gradle', 'groovy', 'node', 'javascript' ]
    }
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact publishPluginJar {
        classifier 'sources'
      }
      artifact publishPluginGroovyDocsJar {
        classifier 'groovydoc'
      }
      artifact publishPluginJavaDocsJar {
        classifier 'javadoc'
      }
    }
  }
}

jacocoTestReport {
  reports {
    xml.enabled = true // coveralls plugin depends on xml format report
    html.enabled = true
  }
}

check.dependsOn 'jacocoTestReport'
publish.mustRunAfter check

tasks.coveralls {
  dependsOn 'check'
  onlyIf { buildEnv.ci }
}

tasks.ci.dependsOn tasks.coveralls

artifactoryPublish.onlyIf { ! buildEnv.release }

artifactory {
  contextUrl = "http://oss.jfrog.org/artifactory"

  publish {
    repository {
      repoKey = 'oss-snapshot-local'
      username = bintrayUser
      password = bintrayKey
      maven = true
    }
  }
}

wrapper.gradleVersion = '2.7'
